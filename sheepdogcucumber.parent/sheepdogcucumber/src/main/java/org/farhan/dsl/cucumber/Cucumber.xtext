grammar org.farhan.dsl.cucumber.Cucumber hidden(WS, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate cucumber "http://www.farhan.org/dsl/cucumber/Cucumber"

Model:
	StepObject | Feature;

StepObject:
	'Object:' name=Title EOL
	statements+=Statement*
	stepDefinitions+=StepDefinition*;

StepDefinition:
	'Definition:' name=Title EOL
	statements+=Statement*
	stepParameters+=StepParameters*;

StepParameters:
	'Parameters:' name=Title EOL
	statements+=Statement*
	parametersTable=ParametersTable;

ParametersTable:
	rows+=Row+;

Feature:
	(tags+=Tag+ EOL)?
	'Feature:' name=Title EOL
	statements+=Statement*
	abstractScenarios+=AbstractScenario*;

// This has the side effect of allowing more than 1 Background.
// Also it allows it to be anywhere, like at the begining or end.
// I'm doing it this way to make the formatting code simpler by having less duplication
AbstractScenario:
	Background | Scenario | ScenarioOutline;

Background:
	'Background:' name=Title EOL
	statements+=Statement*
	steps+=Step*;

Scenario:
	(tags+=Tag+ EOL)?
	'Scenario:' name=Title EOL
	statements+=Statement*
	steps+=Step*;

ScenarioOutline:
	(tags+=Tag+ EOL)?
	'Scenario Outline:' name=Title EOL
	statements+=Statement*
	steps+=Step*
	examples+=Examples+;

Examples:
	(tags+=Tag+ EOL)?
	'Examples:' name=Title EOL
	statements+=Statement*
	theExamplesTable=ExamplesTable;

ExamplesTable:
	rows+=Row+;

StepTable:
	rows+=Row+;

Row:
	cells+=Cell+ '|' EOL;

Cell:
	'|' name=Title;

Step:
	Given | When | Then | And | But | Asterisk;

DocString:
	name=RAWTEXT EOL;
	
Given:
	'Given' name=Title EOL
	(theStepTable=StepTable | theDocString=DocString)?;

When:
	'When' name=Title EOL
	(theStepTable=StepTable | theDocString=DocString)?;

Then:
	'Then' name=Title EOL
	(theStepTable=StepTable | theDocString=DocString)?;

And:
	'And' name=Title EOL
	(theStepTable=StepTable | theDocString=DocString)?;

But:
	'But' name=Title EOL
	(theStepTable=StepTable | theDocString=DocString)?;

Asterisk:
	'*' name=Title EOL
	(theStepTable=StepTable | theDocString=DocString)?;

Statement:
	name=Title EOL;

Title:
	WORD+;

Tag:
	'@' name=WORD;

terminal WS:
	(' ' | '\t' | '\r')+;

terminal SL_COMMENT:
	'#' !('\n')* EOL;

terminal EOL:
	('\n')+;
	
terminal RAWTEXT:
	'"""' '\n' (.)+ '\n' (' ')* '"""';
	
terminal WORD:
	!(' ' | '\t' | '\r' | '\n')+;
	