// TODO try using the built in terminals instead and override them
grammar org.farhan.dsl.sheepdog.SheepDog hidden(WS, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate sheepDog "http://www.farhan.org/dsl/sheepdog"

Model:
	StepObject | TestSuite;

StepObject:
	'=' 'Step-Object:' name=Title EOL
	statementList+=Statement*
	stepDefinitionList+=StepDefinition*;

StepDefinition:
	'==' 'Step-Definition:' name=Title EOL
	statementList+=Statement*
	stepParameterList+=StepParameters*;

StepParameters:
	'*' 'Step-Parameters:' name=Title EOL
	statementList=NestedStatementList?
	table=Table;

TestSuite:
	'=' 'Test-Suite:' name=Title EOL
	statementList+=Statement*
	testStepContainerList+=TestStepContainer*;

TestStepContainer:
	TestSetup | TestCase;

TestSetup:
	'==' 'Test-Setup:' name=Title EOL
	statementList+=Statement*
	testStepList+=TestStep*;

TestCase:
	'==' 'Test-Case:' name=Title EOL
	statementList+=Statement*
	testStepList+=TestStep*
	testDataList+=TestData*;

TestData:
	'*' 'Test-Data:' name=Title EOL
	statementList=NestedStatementList?
	table=Table;

TestStep:
	Given | When | Then | And;

Given:
	'*' 'Given:' object=StepObjectRef predicate=StepDefinitionRef EOL
	(table=Table | text=Text)?;

When:
	'*' 'When:' object=StepObjectRef predicate=StepDefinitionRef EOL
	(table=Table | text=Text)?;

Then:
	'*' 'Then:' object=StepObjectRef predicate=StepDefinitionRef EOL
	(table=Table | text=Text)?;

And:
	'*' 'And:' object=StepObjectRef predicate=StepDefinitionRef EOL
	(table=Table | text=Text)?;

Text:
	'+' EOL
	name=RAWTEXT EOL;

NestedStatementList:
	'+' EOL
	statementList+=Statement+;

Table:
	'+' EOL
	'|===' EOL
	rowList+=Row+
	'|===' EOL;

Row:
	cellList+=Cell+ EOL;

Cell:
	'|' name=Title;

// TODO There need to be better names than Text, Statement, Title, WORD etc ...
Statement:
	name=Title EOL;

Title:
	WORD+;

// TODO the regular expression here is a partial duplication of what's in sheep-dog-test
// It should only be defined here but that is more work than I want to do right now
// When testing sheep-dog-test, it doesn't need a regex.
// It's enough that the Impl classes return what parts it needs.
// If all the parts accessible from the utility are defined here, 
// then the TestStepImpl class + its utility can extract that information
// Also any tests in sheep-dog-test must be implemented in the xtext project
// to make sure the Impl classes have implemented this correctly
// For now I'm putting the whole regex to make it possible to distinguish between STEP_OBJECT and STEP_DEFINITION
// without breaking it up into its parts
// TODO do I need to define single spaces?
StepObjectRef:
	WORD+ ('file' | 'page' | 'response' | 'dialog' | 'directory' | 'request' | 'goal' | 'job' | 'action');

StepDefinitionRef:
	WORD+;

// TODO look at fragments to handle escape characters etc instead of this and to handle re-using ' ' | '\t' | '\r' | '\n'
terminal WORD:
	!(' ' | '\t' | '\r' | '\n' | '|' | '#')
	!(' ' | '\t' | '\r' | '\n')*;

terminal WS:
	(' ' | '\t' | '\r')+;

terminal SL_COMMENT:
	'#' !('\n')* EOL;

terminal EOL:
	('\n')+;

terminal RAWTEXT:
	'----' '\n' (.)+ '\n' '----';
